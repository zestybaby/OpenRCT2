apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '28.0.2'

    defaultConfig {
        applicationId 'website.openrct2'
        minSdkVersion 16
        targetSdkVersion 25

        versionCode 2
        versionName '0.2.1'

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_shared'
                targets 'openrct2', 'openrct2-ui', 'openrct2-cli'
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file('external/debug.keystore')
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
        pr {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        develop {
            applicationIdSuffix '.develop'
            versionNameSuffix '-DEVELOP'
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "version"
    productFlavors {
        full {
            dimension "version"
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/CMakeLists.txt'
        }
    }

    productFlavors {
        arm7 {
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }
        arm {
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
        x86 {
            ndk {
                abiFilters 'x86', 'x86_64'
            }
        }
    }
}

apply plugin: 'de.undercouch.download'

android.applicationVariants.all { variant ->
    variant.mergeAssets.doLast {
        copy {
            from '../../../data'
            into "$variant.mergeAssets.outputDir/data"
        }
        download {
            src 'https://github.com/OpenRCT2/openrct2-dependencies-android/releases/download/v0.8/g2.dat'
            dest "$variant.mergeAssets.outputDir/data"
        }
        download {
            src 'https://github.com/OpenRCT2/title-sequences/releases/download/v0.1.2/title-sequence-v0.1.2.zip'
            dest new File(buildDir, 'title-sequence.zip')
        }
        copy {
            from zipTree(new File(buildDir, 'title-sequence.zip'))
            into "$variant.mergeAssets.outputDir/data/title"
        }
        download {
            src 'https://github.com/OpenRCT2/objects/releases/download/v1.0.8/objects.zip'
            dest new File(buildDir, 'objects.zip')
        }
        copy {
            from zipTree(new File(buildDir, 'objects.zip'))
            into "$variant.mergeAssets.outputDir/data/object"
        }
    }
}

dependencies {
    implementation 'commons-io:commons-io:2.5'
    implementation 'com.android.support:appcompat-v7:25.3.1'
    implementation 'com.google.android.gms:play-services-analytics:10.2.1'
}

apply plugin: 'com.google.gms.google-services'
